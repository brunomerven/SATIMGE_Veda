*--------------------------------------------------------------
*Process TIMES sub-annual results -------------------------------------
*--------------------------------------------------------------
* Bruno - October 2020

* convert DAYNITE to HOURLY and create map
COM_NRGELCR(COM)$NRGELC('REGION1',COM) = yes;

PRC_ANNUAL('REGION1',PRC)$PRC_TSL('REGION1',PRC,'ANNUAL') = yes;
PRC_SEASON('REGION1',PRC)$PRC_TSL('REGION1',PRC,'SEASON') = yes;
PRC_WEEKLY('REGION1',PRC)$PRC_TSL('REGION1',PRC,'WEEKLY') = yes;

*$exit

LOOP(TS_WEEKLY,
    TS_TMP1 = 0;
    TS_TMP2 = 0;

    LOOP(TS_DAYNITE$TS_MAP('REGION1',TS_WEEKLY,TS_DAYNITE),

          TS_Duration(TS_DAYNITE) = round(G_YRFR('REGION1',TS_DAYNITE)*24/G_YRFR('REGION1',TS_WEEKLY));
          TS_TMP2 = TS_TMP1 + TS_Duration(TS_DAYNITE);
          LOOP(TS_HOURLY$(VALHOURLY(TS_HOURLY) > TS_TMP1 and VALHOURLY(TS_HOURLY) <= TS_TMP2),
                 TS_MAP2(TS_WEEKLY,TS_DAYNITE,TS_HOURLY) = Yes;

          );
          TS_TMP1 = TS_TMP2;
    );

);
* Reset all
E_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE) = 0;
E_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE) = 0;
P_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE) = 0;
P_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE) = 0;

* Convert Daynite-type time-sliced flows to DAYNITE
E_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE) = SUM(V,F_IN('REGION1',V,TC,PRC,COM_NRGELCR,TS_DAYNITE));
E_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE) = SUM(V,F_OUT('REGION1',V,TC,PRC,COM_NRGELCR,TS_DAYNITE));

* Convert Annual time-sliced flows to DAYNITE
E_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE)$SUM(V,F_IN('REGION1',V,TC,PRC,COM_NRGELCR,'ANNUAL')) = SUM(V,F_IN('REGION1',V,TC,PRC,COM_NRGELCR,'ANNUAL'))*G_YRFR('REGION1',TS_DAYNITE);
E_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE)$SUM(V,F_OUT('REGION1',V,TC,PRC,COM_NRGELCR,'ANNUAL')) = SUM(V,F_OUT('REGION1',V,TC,PRC,COM_NRGELCR,'ANNUAL'))*G_YRFR('REGION1',TS_DAYNITE);

* Convert Seasonal-type time-sliced flows to DAYNITE
LOOP(TS_SEASON,
    E_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE)$(PRC_SEASON('REGION1',PRC) and TS_MAP('REGION1',TS_SEASON,TS_DAYNITE)) = SUM(V,F_IN('REGION1',V,TC,PRC,COM_NRGELCR,TS_SEASON))*G_YRFR('REGION1',TS_DAYNITE)/G_YRFR('REGION1',TS_SEASON);
    E_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE)$(PRC_SEASON('REGION1',PRC) and TS_MAP('REGION1',TS_SEASON,TS_DAYNITE)) = SUM(V,F_OUT('REGION1',V,TC,PRC,COM_NRGELCR,TS_SEASON))*G_YRFR('REGION1',TS_DAYNITE)/G_YRFR('REGION1',TS_SEASON);
);

* Convert weekly-type time-sliced flows to DAYNITE
LOOP(TS_WEEKLY,
    E_IN(PRC,COM_NRGELCR,TC,TS_DAYNITE)$(PRC_WEEKLY('REGION1',PRC) and TS_MAP('REGION1',TS_WEEKLY,TS_DAYNITE)) = SUM(V,F_IN('REGION1',V,TC,PRC,COM_NRGELCR,TS_WEEKLY))*G_YRFR('REGION1',TS_DAYNITE)/G_YRFR('REGION1',TS_WEEKLY);
    E_OUT(PRC,COM_NRGELCR,TC,TS_DAYNITE)$(PRC_WEEKLY('REGION1',PRC) and TS_MAP('REGION1',TS_WEEKLY,TS_DAYNITE)) = SUM(V,F_OUT('REGION1',V,TC,PRC,COM_NRGELCR,TS_WEEKLY))*G_YRFR('REGION1',TS_DAYNITE)/G_YRFR('REGION1',TS_WEEKLY);
);

* Convert all energy to power
P_IN(PRC,COM,TC,TS_DAYNITE)$E_IN(PRC,COM,TC,TS_DAYNITE) = E_IN(PRC,COM,TC,TS_DAYNITE)/(G_YRFR('REGION1',TS_DAYNITE)*31.536);
P_OUT(PRC,COM,TC,TS_DAYNITE)$E_OUT(PRC,COM,TC,TS_DAYNITE) = E_OUT(PRC,COM,TC,TS_DAYNITE)/(G_YRFR('REGION1',TS_DAYNITE)*31.536);


* Calculate Costs
* Grid Costs
VarCosts_TS(PRCBulkPower,'ELCC',TC,TS_DAYNITE)$REPORT(PRCBulkPower,'ELCC',TC,RUN,'FlowOut') = (REPORT(PRCBulkPower,'ACTGRP',TC,RUN,'FuelCosts')+REPORT(PRCBulkPower,'ACTGRP',TC,RUN,'VOM'))*E_OUT(PRCBulkPower,'ELCC',TC,TS_DAYNITE)/REPORT(PRCBulkPower,'ELCC',TC,RUN,'FlowOut');

AggVarCost_TS('ELC',TC,TS_DAYNITE) = sum(PRCBulkPower,VarCosts_TS(PRCBulkPower,'ELCC',TC,TS_DAYNITE));
AggVarPrice_TS('ELC',TC,TS_DAYNITE)$E_OUT('ETRANS','ELC',TC,TS_DAYNITE) = AggVarCost_TS('ELC',TC,TS_DAYNITE)/E_OUT('ETRANS','ELC',TC,TS_DAYNITE);

* Populate power and price on hourly basis for easier visualization
LOOP(TS_WEEKLY,
         P_IN_H(PRC,COM,TC,TS_WEEKLY,TS_HOURLY) = SUM(TS_DAYNITE$TS_MAP2(TS_WEEKLY,TS_DAYNITE,TS_HOURLY),P_IN(PRC,COM,TC,TS_DAYNITE));
         P_OUT_H(PRC,COM,TC,TS_WEEKLY,TS_HOURLY) = SUM(TS_DAYNITE$TS_MAP2(TS_WEEKLY,TS_DAYNITE,TS_HOURLY),P_OUT(PRC,COM,TC,TS_DAYNITE));
         AggVarPrice_H('ELC',TC,TS_WEEKLY,TS_HOURLY) = SUM(TS_DAYNITE$TS_MAP2(TS_WEEKLY,TS_DAYNITE,TS_HOURLY),AggVarPrice_TS('ELC',TC,TS_DAYNITE));
         Marginal_H('ELC',TC,TS_WEEKLY,TS_HOURLY) = SUM(TS_DAYNITE$TS_MAP2(TS_WEEKLY,TS_DAYNITE,TS_HOURLY),PAR_COMBALEM('REGION1',TC,'ELC',TS_DAYNITE));
);

REPORTH(PRC,COM,TC,TS_WEEKLY,TS_HOURLY,RUN,'FlowIn') = P_IN_H(PRC,COM,TC,TS_WEEKLY,TS_HOURLY);
REPORTH(PRC,COM,TC,TS_WEEKLY,TS_HOURLY,RUN,'FlowOut') = P_OUT_H(PRC,COM,TC,TS_WEEKLY,TS_HOURLY);
REPORTH('ETRANS','ELC',TC,TS_WEEKLY,TS_HOURLY,RUN,'Price') =  AggVarPrice_H('ELC',TC,TS_WEEKLY,TS_HOURLY);
REPORTH('ETRANS','ELC',TC,TS_WEEKLY,TS_HOURLY,RUN,'Marginal') =  Marginal_H('ELC',TC,TS_WEEKLY,TS_HOURLY);

REPORTH_RUN(PRC,COM,TC,TS_WEEKLY,TS_HOURLY,IndicatorsH) = REPORTH(PRC,COM,TC,TS_WEEKLY,TS_HOURLY,RUN,IndicatorsH);

put_utilities Scen 'gdxout' / "H_",RUN.TL:30;
execute_unload REPORTH_RUN;

